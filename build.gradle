plugins {
	id 'fabric-loom' version '0.2.6-SNAPSHOT'
	id 'maven-publish'
	id "co.riiid.gradle" version "0.4.2"
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "com.wynprice.cursemaven" version "2.1.0"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

minecraft {
}

repositories {
	mavenCentral()
	mavenLocal()
	maven{ url="https://minecraft.curseforge.com/api/maven" }
	maven{ url="https://maven.fabricmc.net" }
    maven{ url="http://maven.sargunv.s3-website-us-west-2.amazonaws.com/" }
	maven{ url="http://server.bbkr.space:8081/artifactory/libs-snapshot/" }
	maven{ url="http://server.bbkr.space:8081/artifactory/libs-release/" }
	maven{ url="https://maven.jamieswhiteshirt.com/libs-release/" }
	maven{ url="https://maven.abusedmaster.xyz" }
	maven{ url="https://jitpack.io" }
	maven{ url="https://maven.sk89q.com/repo" }
	maven{ url="https://maven.jamieswhiteshirt.com/libs-release/" }
	maven{ url="https://repo.elytradev.com/" }
	maven{ url="https://maven.swordglowsblue.com/" }
	maven{ url="http://maven.modmuss50.me" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

	modCompile "net.fabricmc:fabric-loader:${project.loader_version}"
	modCompile "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation "com.github.vampire-studios:artifice-unofficial:0.3.7-unofficial"
	include ("com.github.vampire-studios:artifice-unofficial:0.3.7-unofficial") {
		exclude group :"net.fabricmc.fabric-api"
	}

	modImplementation "curse.maven:vampirelib:${project.vampirelib_version}"
	include ("curse.maven:vampirelib:${project.vampirelib_version}") {
		exclude group :"net.fabricmc.fabric-api"
	}

	modCompile "me.sargunvohra.mcmods:autoconfig1u:${project.auto_config_version}"
	include ("me.sargunvohra.mcmods:autoconfig1u:${project.auto_config_version}") {
		exclude group :"net.fabricmc.fabric-api"
	}

	modCompile "me.shedaniel.cloth:config-2:${project.cloth_config_version}"
	include ("me.shedaniel.cloth:config-2:${project.cloth_config_version}") {
		exclude group :"net.fabricmc.fabric-api"
	}

	modApi ("io.github.cottonmc:LibGui:1.3.3-beta.7") { exclude group:"io.github.prospector", module:"modmenu" }

	// For dev env testing
	modImplementation("me.shedaniel:RoughlyEnoughItems:${project.rei_version}") {
		exclude group: "io.github.prospector.modmenu"
		exclude group :"net.fabricmc.fabric-api"
	}
	modImplementation "io.github.prospector:modmenu:${project.modmenu_version}"

	compile "com.google.code.findbugs:jsr305:3.0.2"
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}

File configFile = file('gradle.properties')
task updateFabric {
	doFirst {
		// Get fabric versions/webpage
		def fabricWeb ="https://maven.fabricmc.net/net/fabricmc/"
		def fabricData="maven-metadata.xml"
		def (loader) = "${fabricWeb}fabric-loader/${fabricData}".toURL().text           =~ "<release>.*</release>"
		def (api)    = "${fabricWeb}fabric-api/fabric-api/${fabricData}".toURL().text   =~ "<release>.*</release>"
		def (yarn)   = "${fabricWeb}yarn/${fabricData}".toURL().text                    =~ "<release>.*</release>"
		def loom     = "${fabricWeb}fabric-loom/${fabricData}".toURL().text             =~ "<version>.*</version>"
		// Filter results
		loader = loader.replaceAll("<[/]*release>", "")
		api    = api.replaceAll("<[/]*release>", "")
		yarn   = yarn.replaceAll("<[/]*release>", "")
		loom   = loom[( (int) loom.size())-1].replaceAll("<[/]*version>", "")
		// Print results
		println "yarn   = ${yarn}"
		println "loader = ${loader}"
		println "api    = ${api}"
		println "loom   = ${loom}"

		// Update versions to the latest
		configFile.setText(configFile.getText()
				.replaceFirst("yarn_mappings=1.14.4+build.9", "yarn_mappings=${yarn}")
				.replaceFirst("loader_version=0.4.8+build.159", "loader_version=${loader}")
				.replaceFirst("fabric_version=0.3.1+build.208", "fabric_version=${api}")
		)
	}
}